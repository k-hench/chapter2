---
output: html_document
editor_options:
  chunk_output_type: console
css: highlight.css
---

```{r setup, include = FALSE}
knitr::knit_hooks$set(source = function(x, options) {
  if (!is.null(options$hilang)) {
      code_open <- "\n\n<div class=\"sourceCode\">\n<pre class=\"sourceCode\">\n<code class=\"sourceCode\">"
      code_close <- "\n</code>\n</pre>\n</div>\n"
      code_body <- highr::hi_andre(x, language = options$hilang, format = "html")
    stringr::str_c(
      code_open,
      knitr:::indent_block(paste(code_body, collapse = '\n'), ""),
      code_close
    )
  } else {
    stringr::str_c("\n\n```", tolower(options$engine), "\n",
                   paste(x, collapse = '\n'), "\n```\n\n")

  }
})
knitr::opts_chunk$set(echo = TRUE)
knitr::opts_knit$set(root.dir = '../')
```

# (git 13) Analysis  XI (Whole Genome Phylogenies)

This pipeline can be executed as follows:

```sh
cd $BASE_DIR/nf/13_analysis_phylo_whg
nextflow run analysis_phylo_whg.nf
```

## Summary

The whole genome phylogenies can be reconstructed within the [**nextflow**](https://www.nextflow.io/) script `analysis_phylo_whg.nf` (located under `$BASE_DIR/nf/analysis_phylo_whg/`).

## Details of `analysis_phylo_whg.nf`

> This part of the analysis was actually manged manually and not via `nextflow`. 
> We still report the analysis as a `.nf` script as we believe this is a cleaner and more concise report of the conducted analysis.

### Setup

The nextflow script starts by opening the genotype data and feeding it into two different streams.

:::kclass
```{r , eval = FALSE, hilang = 'nf'}
#!/usr/bin/env nextflow

// ----------------------- DISCLAIMER ----------------------
// this pipeline was not actually run using nexflow,
// but managed manually
// ---------------------------------------------------------

// Hamlet phylogeny
// ----------------

// git 13.1
// open the SNP data set
Channel
	.fromFilePairs("../../1_genotyping/4_phased/phased_mac2.vcf.{gz,gz.tbi}")
	.into{ vcf_hypo_whg_ch; vcf_serr_whg_ch }
```

Next, also a file containing the sample IDs excluding the samples identified as hybrids in git 9 is loaded.

```{r , eval = FALSE, hilang = 'nf'}
// RAxML analysis, Serranus-rooted
// -------------------------------
// git 13.2
// open the sample-list (excluding hybrid samples)
Channel
	.fromPath("../../ressources/samples_hybrids.txt")
	.set{ hybrids_file }
```

As a preparation for running `raxml`, the genotype file is subset to exclude the hybrids.
Then, heterozygous sites are masked and the data is filtered for minimal allele count and physical distance thresholds.
Finally, the genotypes a indirectly converted to `fasta` format.

```{r , eval = FALSE, hilang = 'nf'}
// git 13.3
// subset data and convert to fasta for raxml
process serr_whg_genotypes {
	input:
	set vcfId, file( vcf ), file( hybrids ) from vcf_serr_whg_ch.combine( hybrids_file )

	output:
	file( "hyS_n_0.33_mac4_5kb.fas" ) into raxml_serr_genotypes_ch

	script:
	"""
	# Remove hybrids from genotype data (SNPs only)
	vcftools \
	  --gzvcf  ${vcf[0]} \
	  --remove ${hybrids} \
	  --recode \
	  --stdout | \
	  gzip > hyS.vcf.gz

	# Mask heterozygous genotypes as unknown
	zcat < hyS.vcf.gz | \
	  sed -e s/"1|0"/".|."/g -e s/"0|1"/".|."/g | \
	  gzip > hyS_n.vcf.gz

	# Apply missingness, allele count and distance filters
	vcftools \
	  --gzvcf hyS_n.vcf.gz \
	  --max-missing 0.33 \
	  --mac 4 \
	  --thin 5000 \
	  --recode \
	  --out hyS_n_0.33_mac4_5kb

	# Convert to fasta format (Perl script available at https://github.com/JinfengChen/vcf-tab-to-fasta)
	wget https://raw.githubusercontent.com/JinfengChen/vcf-tab-to-fasta/master/vcf_tab_to_fasta_alignment.pl

	vcf-to-tab < hyS_n_0.33_mac4_5kb.vcf > hyS_n_0.33_mac4_5kb.tab
	
	perl ~/apps/vcf-tab-to-fasta/vcf_tab_to_fasta_alignment.pl -i hyS_n_0.33_mac4_5kb.tab > hyS_n_0.33_mac4_5kb.fas
	"""
}
```

Then, `raxml` can be run on the `fasta` formated genotypes.

```{r , eval = FALSE, hilang = 'nf'}
// git 13.4
// run raxml (Serranus-rooted)
process serr_whg_raxml {
	publishDir "../../2_analysis/raxml/", mode: 'copy' 
	
	input:
	file( fas ) from raxml_serr_genotypes_ch

	output:
	file( "hyS_n_0.33_mac4_5kb.raxml.support" ) into raxml_serr_whg_ch

	script:
	"""
	# Reconstruct phylogeny
	# Note: number of invariant sites for Felsenstein correction was calculated as number of
	# variant sites in alignment (109,660) / genome-wide proportion of variant sites
	# (0.05) * genome-wide proportion of invariant sites (0.95)
	raxml-NG --all \
	  --msa hyS_n_0.33_mac4_5kb.fas \
	  --model GTR+G+ASC_FELS{2083540} \
	  --tree pars{20},rand{20} \
	  --bs-trees 100 \
	  --threads 24 \
	  --worker 4 \
	  --seed 123 \
	  --prefix hyS_n_0.33_mac4_5kb
	"""
}
```

---

The same general aproach is used for the phylogeny excluding the Serranus outgroup samples.
For this, a different sample list (also excluding the outgroup samples) is loaded.

```{r , eval = FALSE, hilang = 'nf'}
// RAxML analysis, floridae-rooted
// -------------------------------
// git 13.5
// open the sample-list (excluding hybrid and Serranus samples)
Channel
	.fromPath("../../ressources/samples_155.txt")
	.set{ hamlet_file }
```

Like in git 13.3, the genotypes are subset and converted to `fasta` format.

```{r , eval = FALSE, hilang = 'nf'}
// git 13.6
// subset data and convert to fasta for raxml
process hypo_whg_genotypes {
	input:
	set vcfId, file( vcf ), file( hamlets ) from vcf_hypo_whg_ch.combine(hamlet_file)

	output:
	file( "hyp155_n_0.33_mac4_5kb.fas" ) into raxml_hypo_genotypes_ch

	script:
	"""
	# Remove hybrid and Serranus samples from genotype data (SNPs only)
	vcftools \
	  --gzvcf ${vcf[0]} \
	  --remove ${hamlets} \
	  --recode \
	  --stdout | \
	  gzip > hyp155.vcf.gz

	# Mask heterozygous genotypes as unknown
	zcat < hyp155.vcf.gz | \
	  sed -e s/"1|0"/".|."/g -e s/"0|1"/".|."/g | \
	  gzip > hyp155_n.vcf.gz

	# Apply missingness, allele count and distance filters
	vcftools \
	  --gzvcf hyp155_n.vcf.gz \
	  --max-missing 0.33 \
	  --mac 4 \
	  --thin 5000 \
	  --recode \
	  --out hyp155_n_0.33_mac4_5kb

	# Convert to fasta format (Perl script available at https://github.com/JinfengChen/vcf-tab-to-fasta)
	wget https://raw.githubusercontent.com/JinfengChen/vcf-tab-to-fasta/master/vcf_tab_to_fasta_alignment.pl

	vcf-to-tab < hyp155_n_0.33_mac4_5kb.vcf > hyp155_n_0.33_mac4_5kb.tab

	perl ./vcf_tab_to_fasta_alignment.pl -i hyp155_n_0.33_mac4_5kb.tab > hyp155_n_0.33_mac4_5kb.fas
	"""
}
```

Finally, again `raxml` is run (equivalent to git 13.4).

```{r , eval = FALSE, hilang = 'nf'}
// git 13.7
// run raxml (floridae-rooted)
process hypo_whg_raxml {
	publishDir "../../2_analysis/raxml/", mode: 'copy' 
	
	input:
	file( fas ) from raxml_hypo_genotypes_ch

	output:
	file( "hyp155_n_0.33_mac4_5kb.raxml.support" ) into raxml_hypo_whg_ch

	script:
	"""
	# Infer phylogeny
	# Note: number of invariant sites for Felsenstein correction was calculated as number of
	# variant sites in alignment (105,043) / genome-wide proportion of variant sites 
	# (0.05) * genome-wide proportion of invariant sites (0.95)

	raxml-NG --all \
	  --msa ${fas} \
	  --model GTR+G+ASC_FELS{1995817} \
	  --tree pars{20},rand{20} \
	  --bs-trees 100 \
	  --threads 24 \
	  --worker 8 \
	  --seed 123 \
	  --prefix hyp155_n_0.33_mac4_5kb
	"""
}

```
:::

---
