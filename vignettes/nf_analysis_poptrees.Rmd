---
output: html_document
editor_options:
  chunk_output_type: console
css: highlight.css
---

```{r setup, include=FALSE}
knitr::knit_hooks$set(source = function(x, options) {
  if (!is.null(options$hilang)) {
      code_open <- "\n\n<div class=\"sourceCode\">\n<pre class=\"sourceCode\">\n<code class=\"sourceCode\">"
      code_close <- "\n</code>\n</pre>\n</div>\n"
      code_body <- highr::hi_andre(x, language = options$hilang, format = "html")
    stringr::str_c(
      code_open,
      knitr:::indent_block(paste(code_body, collapse = '\n'), ""),
      code_close
    )
  } else {
    stringr::str_c("\n\n```", tolower(options$engine), "\n",
                   paste(x, collapse = '\n'), "\n```\n\n")

  }
})
knitr::opts_chunk$set(echo = TRUE)
knitr::opts_knit$set(root.dir = '../')
```


```{r, include=FALSE}
source('R/draw_workflow.R')
prod_poptree <- tibbler(c('outlier_fst_gid_ch', 'outlier_fst_collect_ch'))
```

# (git 7) Analysis V (poptrees)

## Summary

The population recombination rate is estimated within the [**nextflow**](https://www.nextflow.io/) script `analysis_fst_poptree.nf` (located under `$BASE_DIR/nf/07_analysis_fst_poptree/`), which runs on the XX data set.
Below is an overview of the steps involved in the analysis.
(The <span style="color:#4DAF4A">green dot</span> indicates the genotype input, <span style="color:#E41A1C">red arrows</span> depict output that is exported for further use.)

<div style="max-width:500px; margin:auto;">
```{r, echo = FALSE, warning = FALSE, message = FALSE}
girafe( ggobj = dot_plot(file = "docs/analysis_poptree.dot", git = 7, point_types = prod_poptree),
        width_svg = 12, height_svg = 9)
```
</div>

## Details of `analysis_fst_poptree.nf`

### Data preparation

The nextflow script starts by opening the genotype data.

<div class="kclass">
```{r , eval = FALSE, hilang = 'nf'}
#!/usr/bin/env nextflow
// git 7.1
// open genotype data
Channel
	.fromFilePairs("../../1_genotyping/4_phased/phased_mac2.vcf.{gz,gz.tbi}")
	.set{ vcf_fst }
```

Next, all possible population pairs are loaded.

```{r , eval = FALSE, hilang = 'nf'}
// git 7.2
// load all possible population pairs
Channel
	.fromPath("../../ressources/plugin/poptrees/all_crosses.tsv")
	.splitCsv(header:true, sep:"\t")
	.map{ row -> [ pop1:row.pop1, pop2:row.pop2 ] }
	.set{ crosses_ch }
```

Then the <i>F<sub>ST</sub></i> outlier regions are loaded and combined with the population pairs and the genotypes.

```{r , eval = FALSE, hilang = 'nf'}
// git 7.3
// open the focal Fst outlier regions
Channel
	.fromPath("../../ressources/plugin/poptrees/outlier.bed")
	.splitCsv(header:true, sep:"\t")
	.map{ row -> [ chrom:row.chrom, start:row.start, end:row.end, gid:row.gid ] }
	.combine( vcf_fst )
	.combine( crosses_ch )
	.set{ crosses_vcf }
```

Now we have all the information needed to compute the average <i>F<sub>ST</sub></i> within each outlier region for all population pairs.

```{r , eval = FALSE, hilang = 'nf'}
// git 7.4
// compute the average Fst for all possible pair within the outlier region
process outlier_fst {
		label "L_loc_collect_fst"
		publishDir "../../2_analysis/fst/poptree/single", mode: 'copy'

		input:
		set val( grouping ),  val( vcfidx ), file( vcf ), val( cross_pop ) from crosses_vcf

		output:
		set val( grouping.gid ), val( cross_pop ), file( "*.fst.tsv" ) into outlier_fst_gid_ch

		script:
		"""
		echo -e "CHROM\\tSTART\\tEND" > outl.bed
		echo -e "${grouping.chrom}\\t${grouping.start}\\t${grouping.end}" >> outl.bed

		vcfsamplenames ${vcf[0]} | \
			grep "${cross_pop.pop1}" > pop1.pop

			vcfsamplenames ${vcf[0]} | \
				grep "${cross_pop.pop2}" > pop2.pop

		vcftools --gzvcf ${vcf[0]} \
			--bed outl.bed \
			--keep pop1.pop \
			--keep pop2.pop \
			--weir-fst-pop pop1.pop \
			--weir-fst-pop pop2.pop \
			--stdout 2> ${cross_pop.pop1}-${cross_pop.pop2}.50k.log | \
			gzip > ${cross_pop.pop1}-${cross_pop.pop2}.fst.tsv.gz

		mFST=\$(grep "Weir and Cockerham mean Fst estimate:" ${cross_pop.pop1}-${cross_pop.pop2}.50k.log | sed 's/Weir and Cockerham mean Fst estimate: //')
		wFST=\$(grep "Weir and Cockerham weighted Fst estimate:" ${cross_pop.pop1}-${cross_pop.pop2}.50k.log | sed 's/Weir and Cockerham weighted Fst estimate: //')

		echo -e "${cross_pop.pop1}-${cross_pop.pop2}\\t\$mFST\\t\$wFST" > ${cross_pop.pop1}-${cross_pop.pop2}.${grouping.gid}.fst.tsv
		"""
	}
```

The <i>F<sub>ST</sub></i> results for all population pairs are bundled for each outlier region compiled into a single table each.

```{r , eval = FALSE, hilang = 'nf'}
// git 7.5
// collect all population pairs within each region and compile Fst table
process outlier_fst_collect {
		label "L_20g2h_outlier_fst"
		publishDir "../../2_analysis/fst/poptree/summary", mode: 'copy'

		input:
		set val( gid ), val( cross_pop ), file( fst ) from outlier_fst_gid_ch.groupTuple()

		output:
		file( "${gid}.fst.all.tsv" ) into outlier_fst_collect_ch

		script:
		"""
		echo -e "run\\tmean_fst\\tweighted_fst" > ${gid}.fst.all.tsv
		cat *.fst.tsv >> ${gid}.fst.all.tsv
		"""
	}
```

At this point the preparation for the population pairs is completed.
The actual neighbor joining happens later within the plotting script of Figure 4 (`R/fig/plot_F4.R`).

```{r , eval = FALSE, hilang = 'nf'}
// git 7.6
// neighbour joining happens within the R script R/fig/plot_F4.R
```
</div>

---
